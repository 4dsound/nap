// NAP Shader include file

// Light structure for all lights. Not all types use all members.
struct Light
{
	mat4 viewProjectionMatrix;
	vec3 origin;
	vec3 direction;
	vec3 color;
	vec2 nearFar;
	float intensity;
	float attenuation;
	float angle;
	float falloff;
	uint enable;
	uint flags;
};

// Maximum number of supported lights
const uint MAX_LIGHTS			= 8;

// Light types
const uint LIGHT_CUSTOM			= 0;
const uint LIGHT_DIRECTIONAL	= 1;
const uint LIGHT_POINT			= 2;
const uint LIGHT_SPOT			= 3;

// Returns whether the light is enabled
bool isLightEnabled(uint enableFlags)
{
	return (enableFlags & 0x1) > 0;
}

// Returns whether the light is enabled
bool isShadowEnabled(uint enableFlags)
{
	return (enableFlags & 0x02) > 0;
}

// Returns the light type of the specified light flag
uint getLightType(uint flags)
{
	return flags & 0xff;
}


// Returns an incidence value of a light and surface
// @return incidence value [0.0 - 1.0], -1.0 if the light type is not supported
float getSurfaceIncidence(Light light, vec3 surfaceNormal, vec3 surfacePosition)
{
	switch (getLightType(light.flags))
	{
		case LIGHT_DIRECTIONAL:
		{
			return max(dot(surfaceNormal, -light.direction), 0.0);
		}
		case LIGHT_POINT:
		case LIGHT_SPOT:
		{
			vec3 inv_light_direction = normalize(light.origin - surfacePosition);
			return max(dot(surfaceNormal, inv_light_direction), 0.0);
		}

		// Light type not supported
		case LIGHT_CUSTOM:
		default:
		{
			return -1.0;
		}
	}
}
